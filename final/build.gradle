plugins {
    id 'com.gradleup.shadow' version '8.3.4'
}

dependencies {
    implementation project(':shared')
    implementation project(':bukkit')
    implementation project(':bungee')
    implementation project(':api')
    implementation project(':velocity')
}

def static getImplementationConfigurations(parentProject) {
    parentProject.configurations.implementation.canBeResolved = true
    return parentProject.configurations.implementation
}

archivesBaseName = "${rootProject.name}"

tasks.build.dependsOn tasks.shadowJar
shadowJar {
    configurations = [getImplementationConfigurations(project), getImplementationConfigurations(project(':bukkit'))]

    relocate 'org.aopalliance', 'com.lauriethefish.betterportals.dependencies.aopalliance'
    relocate 'org.checkerframework', 'com.lauriethefish.betterportals.dependencies.checkerframework'
    relocate 'org.intellij.lang.annotations', 'com.lauriethefish.betterportals.dependencies.intellij.lang.annotations'
    relocate 'org.jetbrains.annotations', 'com.lauriethefish.betterportals.dependencies.jetbrains.annotations'

    // Unfortunately manually excluding GSON here (it's included with spigot) isn't really possible so we manually relocate these separately
    relocate 'com.google.thirdparty', 'com.lauriethefish.betterportals.dependencies.com.google.thirdparty'
    relocate 'com.google.j2objc', 'com.lauriethefish.betterportals.dependencies.com.google.j2objc'

    relocate 'com.google.inject', 'com.lauriethefish.betterportals.dependencies.com.google.inject'
    relocate 'com.google.errorprone', 'com.lauriethefish.betterportals.dependencies.com.google.errorprone'

    minimize()
}
