plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id "xyz.jpenilla.special-gradle" version "1.0.0-SNAPSHOT"
}

allprojects {
    apply plugin: 'java'
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "com.lauriethefish.betterportals"
    version = '0.10.0-' + "git rev-parse --verify --short HEAD".execute().text.trim().toLowerCase()

    // Optional, just for showing name
    afterEvaluate {
        println("Evaluating project ${project.group}:${archivesBaseName}:${version}")
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileJava.options.encoding = 'UTF-8'

    dependencies {
        implementation 'com.google.inject:guice:5.0.1'
        implementation 'com.google.inject.extensions:guice-assistedinject:5.0.1'
        implementation 'org.jetbrains:annotations:23.0.0'
        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        testImplementation 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor('org.projectlombok:lombok:1.18.22')

        testCompileOnly 'junit:junit:4.13.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.8.1'
    }
}

subprojects {
    archivesBaseName = "${rootProject.name}-${project.name.substring(0, 1).toUpperCase() + project.name.substring(1)}"
}

specialGradle {
    minecraftVersion.set("1.17")
    specialSourceVersion.set("1.10.0")
}

dependencies {
    implementation project(':shared')
    implementation project(':bukkit')
    implementation project(':bungee')
    implementation project(':api')
}

tasks.build.dependsOn tasks.productionMappedJar
shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]

    relocate 'org.bstats', 'com.lauriethefish.bstats'

    // Unfortunately manually excluding GSON here (it's included with spigot) isn't really possible so we manually relocate these separately
    relocate 'com.google.thirdparty', 'com.lauriethefish.google.thirdparty'
    relocate 'com.google.j2objc', 'com.lauriethefish.google.j2objc'
    relocate 'com.google.inject', 'com.lauriethefish.google.inject'
    relocate 'com.google.errorprone', 'com.lauriethefish.google.errorprone'
    relocate 'com.google.common', 'com.lauriethefish.google.common'
}
