plugins {
    id 'java'
}

allprojects {
    apply plugin: 'java'

    group = "com.lauriethefish.betterportals"

    // Optional, just for showing name
    afterEvaluate {
        println("Evaluating project ${project.group}:${archivesBaseName}:${version}")
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileJava.options.encoding = 'UTF-8'

    dependencies {
        implementation 'com.google.inject:guice:5.1.0'
        implementation 'com.google.inject.extensions:guice-assistedinject:5.1.0'
        implementation 'org.jetbrains:annotations:23.0.0'
        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        compileOnly 'org.projectlombok:lombok:1.18.24'
        testImplementation 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    }

    tasks {
        compileJava {
            options.release.set(16)
        }
    }
}

jar.enabled = false
build.enabled = false

subprojects {
    if (System.getenv('BP_DEVELOPER_BUILD') == '1') {
        version = gradle.ext.versionName + '-' + "git rev-parse --verify --short HEAD".execute().text.trim().toLowerCase()
    } else {
        version = gradle.ext.versionName
    }

    archivesBaseName = "${rootProject.name}-${project.name}"
}